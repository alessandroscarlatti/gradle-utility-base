apply plugin: 'groovy'

dependencies {
    compile localGroovy()
}

sourceSets {
    scripts {
        groovy.srcDirs += "scripts"
        compileClasspath += files(sourceSets.main.compileClasspath, sourceSets.main.output)
        runtimeClasspath += files(sourceSets.main.runtimeClasspath, sourceSets.main.output)
    }
}

// create tasks for each script
File[] files = file("scripts").listFiles()
for (File file : files) {
    if (file.name.endsWith(".groovy")) {
        String scriptName = file.name.replace(".groovy", "")
        project.task([type: JavaExec], scriptName) {
            group = "scripts"
            description = "Execute ${scriptName}.groovy"
            it.main = scriptName
            it.classpath = sourceSets.scripts.runtimeClasspath
            doFirst {
                logger.lifecycle("Executing script ${scriptName}.groovy")
            }
        }
    }
}